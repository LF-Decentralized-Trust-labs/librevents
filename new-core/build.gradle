plugins {
    id 'java-library'
    id 'io.freefair.lombok'
}

ext {
    web3jVersion = '4.12.0'
    modelMapperVersion = '3.1.0'
    hashgraphVersion = '2.24.1'
    junitVersion = '5.7.1'
    bcprovVersion = '1.70'
    mockitoVersion = '5.17.0'
    okHttpVersion = '4.12.0'
}

dependencies {
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "org.web3j:core:$web3jVersion"
    implementation "org.bouncycastle:bcprov-jdk15on:$bcprovVersion"
    implementation "org.modelmapper:modelmapper:$modelMapperVersion"

    compileOnly 'org.projectlombok:lombok'

    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test', Test) {
    useJUnitPlatform()
    maxHeapSize = '1G'

    doFirst {
        def bbAgent = configurations.testRuntimeClasspath
                .find { it.name.startsWith('byte-buddy-agent') }
        if (bbAgent) {
            jvmArgs += [
                "-javaagent:${bbAgent.absolutePath}"
            ]
            jvmArgs += [
                "-XX:+EnableDynamicAgentLoading"
            ]
        } else {
            logger.warn("⚠️  byte-buddy-agent JAR not found on testRuntimeClasspath; inline mock-maker may break on future JDKs.")
        }
    }

    testLogging {
        events "passed"
    }
}

tasks.named("spotlessJava").configure {
    dependsOn("spotlessGroovyGradle", "compileJava", "compileTestJava", "javadoc")
}
